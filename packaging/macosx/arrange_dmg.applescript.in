(*
This AppleScript customizes the appearance of the
disk image in which Wireshark is bundled on macOS

 Author:
	Jean-Olivier Irisson <jo.irisson@gmail.com>

Modified by:
	Gerald Combs <gerald@wireshark.org>

 Copyright 2006
 Licensed under GNU General Public License
*)


tell application "Finder"
	tell disk "Wireshark @VERSION@"
		open
		tell container window
			set current view to icon view
			set toolbar visible to false
			set statusbar visible to false
			set the bounds to {600, 200, 950, 725}
		end tell
		set iv_opts to the icon view options of container window
		set background picture of iv_opts to POSIX file "/Volumes/Wireshark @VERSION@/.assets/dmg_background.png"
		tell iv_opts
			set icon size to 72
			set arrangement to not arranged
		end tell
		set position of application file "Wireshark.app" to {80, 64}
		set position of alias file "Applications" to {240, 64}
		set position of file "Read me first.html" to {160, 170}
		set position of alias file "Install ChmodBPF.pkg" to {80, 276}
		set position of alias file "Uninstall ChmodBPF.pkg" to {250, 276}
		set position of alias file "Add Wireshark to the system path.pkg" to {80, 382}
		set position of alias file "Remove Wireshark from the system path.pkg" to {250, 382}
		update without registering applications
	end tell

	--give the finder some time to write the .DS_Store file
	delay 7
end tell
